#version 450 core

#define PI 3.14159265359f
#define JITTER_CONSTANT 0.0001
#define N_SPHERES 0

struct Vertex {
    vec3 position;
    vec3 normal;
};

struct Material {
    vec3 diffuseCol; // 12
    float emissiveStrength; // 4
    float roughness; // 4
    float metallic; // 4
}; // 32 bytes with the padding[2]

struct Ray {
    vec3 ro;
    vec3 rd;
};

struct Sphere {
    vec3 pos;
    float rad;
    Material material;
};

struct Triangle {
    Vertex v0; // 32
    Vertex v1; // 32
    Vertex v2; // 32
    vec3 centroid_; // 12
    int matIndex; // 4
};  // 112 bytes total

struct Node {
    vec3 aabbMin;
    uint leftChild;
    vec3 aabbMax;
    uint rightChild;
    int firstPrim;
    int primCount;
}; // 48 bytes total

struct HitInfo {
    bool hit;
    float dist;
    vec3 pos;
    vec3 normal;
    Material material;
    int triIndex;
};

layout (local_size_x = 8, local_size_y = 8, local_size_z = 1) in;
layout (rgba32f, binding = 0) uniform image2D imgOutput;

layout (std430, binding = 1) readonly buffer trianglesBuffer {
    Triangle[] triangles;
};
layout (std430, binding = 2) readonly buffer nodesBuffer {
    Node[] nodes;
};
layout (std430, binding = 3) readonly buffer materialsBuffer {
    Material[] materials;
};

Sphere spheres[3] = Sphere[3](
    Sphere(vec3(0.25, 0.55, 0.0), 0.05, Material(vec3(0.99, 0.99, 0.99), 0.0, 0.25, 1.0)),
    Sphere(vec3(-0.25, 0.25, -0.25), 0.05, Material(vec3(0.99, 0.99, 0.99), 0.0, 0.0, 0.25)),
    Sphere(vec3(-0.25, 0.88, 0.25), 0.05, Material(vec3(0.99, 0.99, 0.99), 0.0, 0.0, 1.0))
);


uniform samplerCube skybox;
uniform int u_showSkybox;
uniform int u_enableSkybox;
uniform bool u_smoothShading;
uniform float u_skyboxIntensity;
// Render uniforms
uniform int u_frame;
uniform int u_bounces;
uniform int u_samples;
uniform int u_jitter;
// Cam uniforms
uniform vec3 camPos;
uniform mat4 invView;
uniform mat4 invProjection;
// Geometries uniform
uniform int u_nMeshes;
uniform vec2 meshInfo[10];

uniform bool u_gammaCorrection;


// ========= RANDOM FUNCTIONS =========
uint wang_hash(inout uint seed) {
    seed = uint(seed ^ uint(61)) ^ uint(seed >> uint(16));
    seed *= uint(9);
    seed = seed ^ (seed >> 4);
    seed *= uint(0x27d4eb2d);
    seed = seed ^ (seed >> 15);
    return seed;
}

float randomFloat01(inout uint state) {
    return float(wang_hash(state)) / 4294967296.0;
}

vec3 randomUnitVector(inout uint state) {
    float z = randomFloat01(state) * 2.0f - 1.0f;
    float a = randomFloat01(state) * 2.0 * PI;
    float r = sqrt(1.0f - z * z);
    float x = r * cos(a);
    float y = r * sin(a);
    return vec3(x, y, z);
}
// ====================================

// ========= GAMMA CORRECTION =========
vec3 LessThan(vec3 f, float value) {
    return vec3(
        (f.x < value) ? 1.0f : 0.0f,
        (f.y < value) ? 1.0f : 0.0f,
        (f.z < value) ? 1.0f : 0.0f);
}

vec3 LinearToSRGB(vec3 rgb) {
    rgb = clamp(rgb, 0.0f, 1.0f);

    return mix(
        pow(rgb, vec3(1.0f / 2.4f)) * 1.055f - 0.055f,
        rgb * 12.92f,
        LessThan(rgb, 0.0031308f)
    );
}

vec3 SRGBToLinear(vec3 rgb) {
    rgb = clamp(rgb, 0.0f, 1.0f);

    return mix(
        pow(((rgb + 0.055f) / 1.055f), vec3(2.4f)),
        rgb / 12.92f,
        LessThan(rgb, 0.04045f)
    );
}
// ====================================

// ========= RAY SURFACE =========
HitInfo raySphereIntersect(Ray ray, Sphere sph) {
    HitInfo hi;
    hi.hit = false;
    float a = dot(ray.rd, ray.rd);
    vec3 s0_r0 = ray.ro - sph.pos;
    float b = 2.0 * dot(ray.rd, s0_r0);
    float c = dot(s0_r0, s0_r0) - (sph.rad * sph.rad);

    if (b * b - 4.0 * a * c < 0.0) {
        return hi;
    }
    // distance to sphere
    float t = (-b - sqrt((b * b) - 4.0 * a * c)) / (2.0 * a);

    if (t > 0) {
        hi.hit = true;
        hi.dist = t;
        hi.pos = ray.ro + ray.rd * t;
        hi.normal = normalize(hi.pos - sph.pos);
        hi.material = sph.material;
    }
    return hi;
}

HitInfo rayTriangleIntersect(Ray ray, Triangle tri) {
    HitInfo hi;
    hi.hit = false;
    hi.dist = -1.0;

    vec3 v1v0 = tri.v1.position - tri.v0.position;
    vec3 v2v0 = tri.v2.position - tri.v0.position;
    vec3 rov0 = ray.ro - tri.v0.position;
    vec3 n = cross(v1v0, v2v0);

    vec3 q = cross(rov0, ray.rd);
    float d = 1.0 / dot(ray.rd, n);
    float u = d * dot(-q, v2v0);
    float v = d * dot(q, v1v0);
    float t = d * dot(-n, rov0);

    if (u >= 0.0 && v >= 0.0 && (u + v) <= 1.0 && t > 0.0) {
        hi.hit = true;
        hi.dist = t;
        hi.pos = ray.ro + ray.rd * t;
        if (dot(n, ray.rd) > 0.0) {
            n = -n;  // Flip the normal if it's not facing the ray origin
        }
        hi.normal = normalize(n);
        hi.material = materials[tri.matIndex];
    }

    return hi;
}

float rayAABBIntersect(Ray ray, vec3 bmin, vec3 bmax) {
    float tx1 = (bmin.x - ray.ro.x) / ray.rd.x;
    float tx2 = (bmax.x - ray.ro.x) / ray.rd.x;
    float tmin = min(tx1, tx2);
    float tmax = max(tx1, tx2);
    float ty1 = (bmin.y - ray.ro.y) / ray.rd.y;
    float ty2 = (bmax.y - ray.ro.y) / ray.rd.y;
    tmin = max(tmin, min(ty1, ty2));
    tmax = min(tmax, max(ty1, ty2));
    float tz1 = (bmin.z - ray.ro.z) / ray.rd.z;
    float tz2 = (bmax.z - ray.ro.z) / ray.rd.z;
    tmin = max(tmin, min(tz1, tz2));
    tmax = min(tmax, max(tz1, tz2));
    bool hit = tmax >= tmin && tmax > 0;
    return hit ? tmin : 1e10;
}


HitInfo rayPlaIntersect(Ray ray, in vec4 p)
{
    HitInfo hi;
    hi.hit = false;

    float denom = -(dot(ray.ro, p.xyz) + p.w) / dot(ray.rd, p.xyz);

    if (denom > 0.0) {
        hi.pos = ray.ro + denom * ray.rd;
        hi.dist = denom;
        hi.normal = p.xyz;
        hi.hit = true;
    }
    return hi;
}

vec3 calculateBarycentric(vec3 pos, vec3 a, vec3 b, vec3 c)
{
    vec3 v0 = b - a; vec3 v1 = c - a; vec3 v2 = pos - a;
    float d00 = dot(v0, v0);
    float d01 = dot(v0, v1);
    float d11 = dot(v1, v1);
    float d20 = dot(v2, v0);
    float d21 = dot(v2, v1);
    float denom = d00 * d11 - d01 * d01;
    vec3 bar;
    bar.y = (d11 * d20 - d01 * d21) / denom;
    bar.z = (d00 * d21 - d01 * d20) / denom;
    bar.x = 1.0f - bar.y - bar.z;
    return bar;
}
// ====================================


HitInfo intersects(Ray ray) {
    HitInfo hi;
    hi.dist = 1e10;
    hi.hit = false;

    // sphere data does not come from the CPU, store as shader constant
    // this became more of a debug tool now
    for (int i = 0; i < N_SPHERES; i++) {
        HitInfo sphereHi = raySphereIntersect(ray, spheres[i]);
        if (sphereHi.hit && (sphereHi.dist < hi.dist)) {
            hi = sphereHi;
            hi.triIndex = -1;
        }
    }

    // BVH : 12/06 discovered the stack method instead of the hit link and miss method, s/o Sebastian Lague
    // every mesh has its own bounding volume hierarchy
    for (int m = 0; m < u_nMeshes; m++) {
        // extract the mesh offset from meshInfo
        int meshOffset = int(meshInfo[m].x);

        // "Pile" in French
        // the stack containes indices instead of nodes directly in order to minimize data transfer weight
        uint stack[32];
        int currStackIndex = 0;
        stack[currStackIndex++] = 0;

        while (currStackIndex > 0) {
            // we only offset the node index on read
            Node node = nodes[stack[--currStackIndex] + meshOffset];

            // the node is a leaf
            if (node.primCount > 0) {
                for (int i = node.firstPrim; i < node.firstPrim + node.primCount; i++) {
                    HitInfo triangleHi = rayTriangleIntersect(ray, triangles[i]);
                    if (triangleHi.hit && (triangleHi.dist < hi.dist)) {
                        hi = triangleHi;
                        hi.triIndex = meshInfo[m].y > 0.0 ? i : -1;
                    }
                }
            } else {
                // The closest child will be looked at first
                Node left = nodes[node.leftChild + meshOffset];
                Node right = nodes[node.rightChild + meshOffset];
                float distLeft = rayAABBIntersect(ray, left.aabbMin, left.aabbMax);
                float distRight = rayAABBIntersect(ray, right.aabbMin, right.aabbMax);

                bool isLeftNearest = distLeft < distRight;
                float distNear = isLeftNearest ? distLeft : distRight;
                float distFar = isLeftNearest ? distRight : distLeft;

                if (distFar < hi.dist) stack[currStackIndex++] = isLeftNearest ? node.rightChild : node.leftChild;
                if (distNear < hi.dist) stack[currStackIndex++] = isLeftNearest ? node.leftChild : node.rightChild;
            }
        }
    }

    return hi;
}


vec3 trace(Ray ray, inout uint seed) {
    vec3 rayCol = vec3(1.);
    vec3 pixelColor = vec3(0.);

    for (int i = 0; i < u_bounces; i++) {
        HitInfo hi = intersects(ray);
        if (!hi.hit) {
            vec3 skyColor = texture(skybox, ray.rd).rgb;
            if(u_gammaCorrection)
                    skyColor = SRGBToLinear(skyColor);

            // reminder :  u_enableSkybox = 0 or 1
            //             clamp(i, u_showSkybox, 1) will show the skybox only in the reflections if u_showSkybox is 0
            pixelColor += skyColor * u_skyboxIntensity * rayCol * u_enableSkybox * clamp(i, u_showSkybox, 1);
            break;
        }
        // we assumed that emissiveCol is the same as diffuseCol for the emissive objects (1 less mat component to share)
        //                    emitted light                                 the incomming light will be tinted by all the objects
        //                                                                  that have been hit so far (explaing the * col)
        pixelColor += (hi.material.diffuseCol * hi.material.emissiveStrength) * rayCol;
        rayCol *= hi.material.diffuseCol;

        // smooth shading : only happens if the hit triangle and the uniform allow it
        if (hi.triIndex > 0 && u_smoothShading) {
            Triangle tri = triangles[hi.triIndex];
            vec3 bar = calculateBarycentric(hi.pos, tri.v0.position, tri.v1.position, tri.v2.position);
            hi.normal = bar.x * tri.v0.normal + bar.y * tri.v1.normal + bar.z * tri.v2.normal;
        }

        Ray newRay;
        newRay.ro = hi.pos + hi.normal * 0.001;

        // roughness component
        // the bigger the roughness, the more the ray will be reflected in a diffuse manner
        // instead of following the specular direction
        vec3 diffuseDir = normalize(hi.normal + randomUnitVector(seed));
        vec3 specularReflectionDir = reflect(ray.rd, hi.normal);
        specularReflectionDir = normalize(mix(specularReflectionDir, diffuseDir, hi.material.roughness));

        // the less metallic the component is, the less specular reflection there will be
        float shouldSpecular = (randomFloat01(seed) < hi.material.metallic) ? 1.0f : 0.0f;
        newRay.rd = normalize(mix(diffuseDir, specularReflectionDir, shouldSpecular));

        // early stoppage
        // https://blog.demofox.org/2020/06/06/casual-shadertoy-path-tracing-2-image-improvement-and-glossy-reflections/
        float p = max(rayCol.r, max(rayCol.g, rayCol.b));
        if (randomFloat01(seed) > p) {
            break;
        }
        // Add the energy we 'lose' by randomly terminating paths
        rayCol *= 1.0f / p;


        ray = newRay;
    }

    return pixelColor;
}

void main() {
    ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy);
    // here gl_NumWorkGroups is basically resolution
    vec2 uv;
    uv.x = (float(texelCoord.x) - .5 * gl_NumWorkGroups.x * gl_WorkGroupSize.x);
    uv.y = (float(texelCoord.y) - .5 * gl_NumWorkGroups.y * gl_WorkGroupSize.x);
    uv /= gl_WorkGroupSize.xy * gl_NumWorkGroups.xy;

    Ray cam;
    cam.ro = camPos;
    // apply the inv projection and view matrices to the classical RT ray direction
    vec4 target = invProjection * vec4(uv.x, uv.y, 1.0, 1.0);
    vec3 normalizedTarget = normalize(vec3(target) / target.w);
    cam.rd = vec3(invView * vec4(normalizedTarget, 0.0));

    vec3 col;
    // mulitple rays per pixel
    for (int i = 0; i < u_samples; i++) {
        uint seed = uint(uint(texelCoord.x) * uint(1973) + uint(texelCoord.y) * uint(9277) + uint(u_frame) * uint(26699)) | uint(i);
        Ray randomCam;
        randomCam.ro = cam.ro;
        randomCam.rd = normalize(cam.rd + u_jitter * JITTER_CONSTANT * randomUnitVector(seed));
        col += trace(randomCam, seed) / float(u_samples);
    }


    // accumulation
    vec3 colPrev = imageLoad(imgOutput, texelCoord).rgb;
    col = mix(colPrev, col, 1.0 / (float(u_frame) + 1));


    imageStore(imgOutput, texelCoord, vec4(col, 1.0));
}
#version 450 core

out vec4 FragColor;
in vec2 TexCoords;
// texture written by the compute shader
uniform sampler2D tex;
uniform bool u_gammaCorrection;
uniform bool u_toneMapping;
uniform float u_exposure;

vec3 LessThan(vec3 f, float value) {
    return vec3(
        (f.x < value) ? 1.0f : 0.0f,
        (f.y < value) ? 1.0f : 0.0f,
        (f.z < value) ? 1.0f : 0.0f);
}

vec3 LinearToSRGB(vec3 rgb) {
    rgb = clamp(rgb, 0.0f, 1.0f);

    return mix(
        pow(rgb, vec3(1.0f / 2.4f)) * 1.055f - 0.055f,
        rgb * 12.92f,
        LessThan(rgb, 0.0031308f)
    );
}

vec3 SRGBToLinear(vec3 rgb) {
    rgb = clamp(rgb, 0.0f, 1.0f);

    return mix(
        pow(((rgb + 0.055f) / 1.055f), vec3(2.4f)),
        rgb / 12.92f,
        LessThan(rgb, 0.04045f)
    );
}

vec3 ACESFilm(vec3 x)
{
    float a = 2.51f;
    float b = 0.03f;
    float c = 2.43f;
    float d = 0.59f;
    float e = 0.14f;
    return clamp((x*(a*x + b)) / (x*(c*x + d) + e), 0.0f, 1.0f);
}

void main()
{
    vec3 col = texture(tex, TexCoords).rgb;

    col *= u_exposure;

    if(u_toneMapping)
        col = ACESFilm(col);

    if(u_gammaCorrection)
        col = LinearToSRGB(col);

    FragColor = vec4(col, 1.0);
}
